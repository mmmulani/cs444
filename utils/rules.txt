Type PrimitiveType
Type ReferenceType
PrimitiveType boolean
PrimitiveType byte
PrimitiveType short
PrimitiveType int
PrimitiveType char
ReferenceType ClassOrInterfaceType
ReferenceType ArrayType
ClassOrInterfaceType Identifiers
InterfaceType Identifiers
ArrayType PrimitiveType [ ]
ArrayType Identifiers [ ]
Identifiers Identifier
Identifiers Identifiers . Identifier
CompilationUnit PackageDeclaration_OPT ImportDeclarations_OPT TypeDeclaration_OPT
ImportDeclarations ImportDeclaration
ImportDeclarations ImportDeclarations ImportDeclaration
PackageDeclaration package Identifiers ;
ImportDeclaration SingleTypeImportDeclaration
ImportDeclaration TypeImportOnDemandDeclaration
SingleTypeImportDeclaration import Identifiers ;
TypeImportOnDemandDeclaration import Identifiers . * ;
TypeDeclaration ClassDeclaration Semi_OPT
TypeDeclaration InterfaceDeclaration Semi_OPT
TypeDeclaration ;
Semi ;
Modifiers Modifier
Modifiers Modifiers Modifier
Modifier public
Modifier protected
Modifier abstract
Modifier static
Modifier final
Modifier native
ClassDeclaration Modifiers_OPT class Identifier Super_OPT Interfaces_OPT ClassBody
Super extends ClassType
ClassType Identifiers
Interfaces implements InterfaceTypeList
InterfaceTypeList InterfaceType
InterfaceTypeList InterfaceTypeList , InterfaceType
ClassBody { ClassBodyDeclarations_OPT }
ClassBodyDeclarations ClassBodyDeclaration
ClassBodyDeclarations ClassBodyDeclarations ClassBodyDeclaration
ClassBodyDeclaration ClassMemberDeclaration
ClassBodyDeclaration ConstructorDeclaration
ClassMemberDeclaration FieldDeclaration
ClassMemberDeclaration MethodDeclaration
ClassMemberDeclaration ;
FieldDeclaration Modifiers_OPT Type VariableDeclarator ;
MethodDeclaration MethodHeader MethodBody
MethodHeader Modifiers_OPT Type MethodDeclarator
MethodHeader Modifiers_OPT void MethodDeclarator
MethodDeclarator Identifier ( FormalParameterList_OPT ) Dim_OPT
FormalParameterList FormalParameter
FormalParameterList FormalParameterList , FormalParameter
FormalParameter Type Identifier
ClassTypeList ClassType
ClassTypeList ClassTypeList , ClassType
MethodBody Block
MethodBody ;
ConstructorDeclaration Modifiers_OPT ConstructorDeclarator ConstructorBody
ConstructorDeclarator Identifier ( FormalParameterList_OPT )
ConstructorBody Block
InterfaceDeclaration Modifiers_OPT interface Identifier ExtendsInterfaces_OPT InterfaceBody
ExtendsInterfaces extends InterfaceType
ExtendsInterfaces ExtendsInterfaces , InterfaceType
InterfaceBody { InterfaceMemberDeclarations_OPT }
InterfaceMemberDeclarations InterfaceMemberDeclaration
InterfaceMemberDeclarations InterfaceMemberDeclarations InterfaceMemberDeclaration
InterfaceMemberDeclaration AbstractMethodDeclaration
InterfaceMemberDeclaration ;
AbstractMethodDeclaration Modifiers_OPT Type MethodDeclarator ;
AbstractMethodDeclaration Modifiers_OPT void MethodDeclarator ;
Block { BlockStatements_OPT }
BlockStatements BlockStatement
BlockStatements BlockStatements BlockStatement
BlockStatement LocalVariableDeclarationStatement
BlockStatement Statement
LocalVariableDeclarationStatement LocalVariableDeclaration ;
LocalVariableDeclaration Type VariableDeclarator
VariableDeclarator Identifier = Expression
VariableDeclarator Identifier
Statement StatementWithoutTrailingSubstatement
Statement IfThenStatement
Statement IfThenElseStatement
Statement WhileStatement
Statement ForStatement
StatementWithoutTrailingSubstatement Block
StatementWithoutTrailingSubstatement EmptyStatement
StatementWithoutTrailingSubstatement ExpressionStatement
StatementWithoutTrailingSubstatement ReturnStatement
StatementNoShortIf StatementWithoutTrailingSubstatement
StatementNoShortIf IfThenElseStatementNoShortIf
StatementNoShortIf WhileStatementNoShortIf
StatementNoShortIf ForStatementNoShortIf
IfThenStatement if ( Expression ) Statement
IfThenElseStatement if ( Expression ) StatementNoShortIf else Statement
IfThenElseStatementNoShortIf if ( Expression ) StatementNoShortIf else StatementNoShortIf
EmptyStatement ;
ExpressionStatement StatementExpression ;
StatementExpression Assignment
StatementExpression MethodInvocation
StatementExpression ClassInstanceCreationExpression
WhileStatement while ( Expression ) Statement
WhileStatementNoShortIf while ( Expression ) StatementNoShortIf
ForStatement for ( ForInit_OPT ; Expression_OPT ; ForUpdate_OPT ) Statement
ForStatementNoShortIf for ( ForInit_OPT ; Expression_OPT ; ForUpdate_OPT ) StatementNoShortIf
ForInit StatementExpression
ForInit LocalVariableDeclaration
ForUpdate StatementExpression
ReturnStatement return Expression_OPT ;
Comma ,
Primary PrimaryNoNewArray
Primary ArrayCreationExpression
PrimaryNoNewArray Literal
PrimaryNoNewArray this
PrimaryNoNewArray ( Expression )
PrimaryNoNewArray ClassInstanceCreationExpression
PrimaryNoNewArray FieldAccess
PrimaryNoNewArray MethodInvocation
PrimaryNoNewArray ArrayAccess
ClassInstanceCreationExpression new ClassType ( ArgumentList_OPT )
ArgumentList Expression
ArgumentList ArgumentList , Expression
ArrayCreationExpression new PrimitiveType [ Expression ]
ArrayCreationExpression new Identifiers [ Expression ]
Dim [ ]
FieldAccess Primary . Identifier
FieldAccess super . Identifier
MethodInvocation Identifiers ( ArgumentList_OPT )
MethodInvocation Primary . Identifier ( ArgumentList_OPT )
ArrayAccess Identifiers [ Expression ]
ArrayAccess PrimaryNoNewArray [ Expression ]
PostfixExpression Primary
PostfixExpression Identifiers
UnaryExpression - UnaryExpression
UnaryExpression UnaryExpressionNotPlusMinus
UnaryExpressionNotPlusMinus PostfixExpression
UnaryExpressionNotPlusMinus ! UnaryExpression
UnaryExpressionNotPlusMinus CastExpression
CastExpression ( PrimitiveType Dim_OPT ) UnaryExpression
CastExpression ( Expression ) UnaryExpressionNotPlusMinus
CastExpression ( Identifiers Dim ) UnaryExpressionNotPlusMinus
MultiplicativeExpression UnaryExpression
MultiplicativeExpression MultiplicativeExpression * UnaryExpression
MultiplicativeExpression MultiplicativeExpression / UnaryExpression
MultiplicativeExpression MultiplicativeExpression % UnaryExpression
AdditiveExpression MultiplicativeExpression
AdditiveExpression AdditiveExpression + MultiplicativeExpression
AdditiveExpression AdditiveExpression - MultiplicativeExpression
RelationalExpression AdditiveExpression
RelationalExpression RelationalExpression < AdditiveExpression
RelationalExpression RelationalExpression > AdditiveExpression
RelationalExpression RelationalExpression <= AdditiveExpression
RelationalExpression RelationalExpression >= AdditiveExpression
RelationalExpression RelationalExpression instanceof ReferenceType
EqualityExpression RelationalExpression
EqualityExpression EqualityExpression == RelationalExpression
EqualityExpression EqualityExpression != RelationalExpression
AndExpression EqualityExpression
AndExpression AndExpression & EqualityExpression
ExclusiveOrExpression AndExpression
ExclusiveOrExpression ExclusiveOrExpression ^ AndExpression
InclusiveOrExpression ExclusiveOrExpression
InclusiveOrExpression InclusiveOrExpression | ExclusiveOrExpression
ConditionalAndExpression InclusiveOrExpression
ConditionalAndExpression ConditionalAndExpression && InclusiveOrExpression
ConditionalOrExpression ConditionalAndExpression
ConditionalOrExpression ConditionalOrExpression || ConditionalAndExpression
ConditionalExpression ConditionalOrExpression
AssignmentExpression ConditionalExpression
AssignmentExpression Assignment
Assignment LeftHandSide = AssignmentExpression
LeftHandSide Identifiers
LeftHandSide FieldAccess
LeftHandSide ArrayAccess
Expression AssignmentExpression
